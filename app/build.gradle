apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.mob.sdk'
apply plugin: 'com.getkeepsafe.dexcount'

MobSDK {
    appKey "238fb53ee1158"
    appSecret "504a561776accf7fd146af8908a85837"

    ShareSDK {
        devInfo {
            QQ {
                sortId 0
                appId "100371282"
                appKey "aed9b0303e3ed1e27bae87c33761161d"
                shareByAppClient true
                bypassApproval true
            }
            Wechat {
                sortId 1
                appId "wx4868b35061f87885"
                appSecret "64020361b8ec4c99936c0e3999a9f249"
                shareByAppClient true
                bypassApproval true
            }
        }
    }
}

android {
    signingConfigs {
        config {
            keyAlias 'cwq'
            keyPassword '123456'
            storeFile file('../develop.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "cn.chhd.news"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
}

kapt {
    generateStubs = true
}

// 解决支持库版本兼容问题 (http://www.jcodecraeer.com/a/anzhuokaifa/androidkaifa/2017/0910/8491.html)
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion rootProject.ext.supportLibVersion
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation project(':lib-my')
    implementation 'com.android.support:multidex:1.0.2'
    // RxAndroid (https://github.com/ReactiveX/RxAndroid)
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    // Retrofit (https://github.com/square/retrofit)
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    // OkHttp (https://github.com/square/okhttp)
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    // Stetho (https://github.com/facebook/stetho)
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    // Dagger 2 (https://github.com/google/dagger)
    implementation 'com.google.dagger:dagger:2.13'
    kapt 'com.google.dagger:dagger-compiler:2.13'
    // Glide (https://github.com/bumptech/glide)
    implementation 'com.github.bumptech.glide:glide:4.1.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'
    // RxBinding (https://github.com/JakeWharton/RxBinding)
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    // google-gson (https://github.com/google/gson)
    implementation 'com.google.code.gson:gson:2.8.2'
    // BaseRecyclerViewAdapterHelper (https://github.com/CymChad/BaseRecyclerViewAdapterHelper)
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    // AndroidUtilCode (https://github.com/Blankj/AndroidUtilCode)
    implementation 'com.blankj:utilcode:1.10.0'
    // Slidr (https://github.com/r0adkll/Slidr)
    implementation 'com.r0adkll:slidableactivity:2.0.6'
    // Material Dialogs (https://github.com/afollestad/material-dialogs)
    implementation 'com.afollestad.material-dialogs:commons:0.9.4.3'
    // LicensesDialog (https://github.com/PSDev/LicensesDialog)
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.2'
    // Bugly (https://bugly.qq.com/v2/)
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.4'
    // CircleImageView (https://github.com/hdodenhof/CircleImageView)
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    // RxLifecycle (https://github.com/trello/RxLifecycle)
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.1'
    // FlexboxLayout (https://github.com/google/flexbox-layout)
    implementation 'com.google.android:flexbox:0.3.2'
    // PhotoView (https://github.com/chrisbanes/PhotoView)
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
}
